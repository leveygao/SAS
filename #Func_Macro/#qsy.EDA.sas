options ls=255 ps=500 yearcutoff=1920 sortsize=max sumsize=max bufno=50 nosource nonumber nocenter nodate replace;
options notes ovp mprint symbolgen=n;
options compress=yes noerrorabend;
options formchar='|-+++++++++++=|-/\<>*';

%macro explore(dt);
proc contents data=&dt. (drop=&drpvl.) out=t(keep=varnum name label type length format) noprint;
run;
proc sort data=t out=t nodup;
  by varnum;
run;
proc print data=t noobs;
  title "Data File: &dt.";
  var varnum name type length format label;
run;
proc print data=&dt. &iif.;
  title "Data File: &dt. &iif.";
run;
data t_d t_n t_c;
  set t end=last;
  retain td tn tc 0;
  drop td tn tc;
  if upcase(substr(left(format),1,4))="YYMM" or upcase(substr(left(format),1,4))="DDMM" then do;  /*Time*/
     td+1;
     output t_d;
  end;
  else if type=1 then do;
     tn+1;
     output t_n;
  end;
  else if type=2 then do;
     tc+1;
     output t_c;
  end;
  if last then do;
     call symput("td", td);
     call symput("tn", tn);
     call symput("tc", tc);
  end;
run;
%if %eval(&td.>0) %then %do;
    proc sort data=t_d nodup;
      by varnum;
    run;
    data _null_;
      set t_d end=last;
      call symput("dv"||compress(_n_),trim(left(name)));
      if last then call symput("td",compress(_n_));
    run;
    %do j=1 %to &td.;
        data &&dv&j.. ;
          length &&dv&j.. 8;
          format &&dv&j.. 8.;
          set &dt. (keep= &&dv&j.. );
          &&dv&j..=input(put(&&dv&j.., YYMMDDn8.), $8.);
        run;
        proc means data=&&dv&j..  n nmiss mean min p1 p5 p10 p25 p50 p75 p90 p95 p99 max;
          title "Data File: &dt. # &j.th Date Var: &&dv&j..";
        run;
    %end;
%end;
%if %eval(&tn.>0) %then %do;
    proc sort data=t_n nodup;
      by varnum;
    run;
    data _null_;
      set t_n end=last;
      call symput("nv"||compress(_n_),trim(left(name)));
      if last then call symput("tn",compress(_n_));
    run;
    %do j=1 %to &tn.;
        proc means data=&dt.(keep= &&nv&j.. ) n nmiss mean std min p1 p5 p10 p25 p50 p75 p90 p95 p99 max;
          title "Data File: &dt. # &j.th Num Var: &&nv&j..";
        run;
    %end;
%end;
%if %eval(&tc.>0) %then %do;
    proc sort data=t_c nodup;
      by varnum;
    run;
    data _null_;
      set t_c end=last;
      call symput("cv"||compress(_n_),trim(left(name)));
      if last then call symput("tc",compress(_n_));
    run;
    %do j=1 %to &tc.;
        proc freq data=&dt.(keep= &&cv&j.. ) noprint;
          table &&cv&j.. /out=Freq&j. missing;
        run;
        data Freq&j.;
          set Freq&j.;
          if _n_<=300 or percent>=1;
        run;
        proc print data=Freq&j. ;
          title "Data File: &dt. # &j.th Char Var: &&cv&j..";
        run;
    %end;
%end;
%mend explore;


libname dev ".";

%let iif=%str((obs=20));

ods html file="EDA_brr.html";

%let drpvl=;
%explore(dev.brr);

ods html close;
