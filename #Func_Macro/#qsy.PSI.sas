options ls=255 ps=500 yearcutoff=1920 sortsize=max sumsize=max bufno=50 nosource nonumber nocenter nodate replace;
options notes ovp mprint symbolgen=n;
options compress=yes noerrorabend;
options formchar='|-+++++++++++=|-/\<>*';


%let speval1=.;
%let speval2=0;
%let nspeval=2;

%MACRO PSINUM(DATADEV,DATAVLD,DATAOUT,NUMVAR,GROUP,WT);
DATA MTEMP1;
  SET &DATADEV. (KEEP=&NUMVAR. &WT.);
  %if %eval(&nspeval.>0) %then %do;
    WHERE &NUMVAR. not in (%do i=1 %to %eval(&nspeval.);   &&speval&i..   %end;);
  %end;
RUN;
PROC SORT DATA=MTEMP1;
  BY &NUMVAR.;
RUN;
PROC SUMMARY DATA=MTEMP1;
  VAR &WT.;
  OUTPUT OUT=T1 SUM(&WT.)=TOTWGT;
RUN;
DATA MTEMP2;
  IF _N_=1 THEN SET T1;
  SET MTEMP1;
  BY &NUMVAR.;
  RETAIN RANK;
  CUMN+&WT. ;
  IF FIRST.&NUMVAR. THEN RANK = INT(&GROUP.*(CUMN-&WT.) / TOTWGT) +1;
  keep RANK &NUMVAR.;
RUN;
PROC SORT DATA=MTEMP2;
  BY RANK &NUMVAR.;
RUN;
DATA MTEMP3;
  SET MTEMP2;
  BY RANK &NUMVAR.;
  IF LAST.RANK;
RUN;
DATA _NULL_;
  FILE "FMT" LRECL=32767;
  SET MTEMP3 END=LAST;
  IF _N_=1 THEN DO;
     PUT '%MACRO FMT;';
     PUT '  LENGTH RANK $ 16;';
     %if %eval(&nspeval.>0) %then %do;
         %do i=1 %to %eval(&nspeval.);
             PUT "  IF &NUMVAR. IN (&&speval&i..) THEN RANK=" '"-' '"||PUT(' "&i.,Z2.)||" '": ' "&&speval&i.." '"; ELSE';
         %end;
     %end;
  END;
  PUT "  IF &NUMVAR. <=" &NUMVAR. " THEN RANK=PUT(" RANK ',Z3.)||"' ': <="||COMPRESS(' &NUMVAR.'); ELSE';
  IF LAST THEN DO;
     PUT "  RANK=PUT(" RANK ',Z3.)||"' ': <="||COMPRESS(' &NUMVAR.');';
     PUT '%MEND;';
  END;
RUN;
%INCLUDE "FMT";
DATA MTEMP4;
  SET &DATADEV. (KEEP=&NUMVAR. &WT.);
  %FMT;
RUN;
DATA MTEMP5;
  SET &DATAVLD. (KEEP=&NUMVAR. &WT.);
  %FMT;
RUN;
DATA MTEMP6;
  SET MTEMP4(IN=A) MTEMP5;
  IF A THEN TMFLAG='D'; ELSE TMFLAG='T';
RUN;
title "PSI OF &NUMVAR. Between Datasets: &DATADEV. vs. &DATAVLD.";
PROC FREQ DATA=MTEMP6;
  TABLE RANK*TMFLAG /MISSING OUT=TEM1;
  WEIGHT &WT.;
RUN;
title;
DATA TEM2 TEM3;
  SET TEM1;
  IF TMFLAG='D' THEN OUTPUT TEM2;
  ELSE IF TMFLAG='T' THEN OUTPUT TEM3;
  DROP TMFLAG;
RUN;
PROC SORT DATA=TEM2;
BY RANK;
RUN;
PROC SORT DATA=TEM3(RENAME=(PERCENT=PERCENT1 COUNT=COUNT1));
BY RANK;
RUN;
DATA TEM4;
  MERGE TEM2 TEM3;
  BY RANK;
RUN;
DATA TEM5;
  SET TEM4;
  ARRAY A COUNT COUNT1;
  DO OVER A;
     IF A=. THEN A=0;
  END;
RUN;
PROC SUMMARY DATA=TEM5;
  VAR COUNT COUNT1;
  OUTPUT OUT=TEM6 SUM(COUNT COUNT1)=COUNTTOT COUNT1TOT;
RUN;
DATA TEM7;
  IF _N_=1 THEN SET TEM6;
  SET TEM5;
  PERCENT=MAX(COUNT,0.1)/COUNTTOT;
  PERCENT1=MAX(COUNT1,0.1)/COUNT1TOT;
RUN;
DATA TEM8;
  SET TEM7;
  TOT=LOG(PERCENT/PERCENT1)*((PERCENT-PERCENT1));
RUN;
PROC SUMMARY DATA=TEM8;
  VAR TOT;
  OUTPUT OUT=RESLT SUM(TOT)=TOT;
RUN;
DATA RESLT2(KEEP=PSI NAME LABEL);
  LENGTH NAME $ 64 LABEL $ 100;
  SET RESLT;
  NAME="&NUMVAR.";
  LABEL="&LABEL.";
  RENAME TOT=PSI;
RUN;
PROC APPEND BASE=&DATAOUT.PSINUM DATA=RESLT2 FORCE;
RUN;
PROC DATASETS NOLIST LIB=WORK;
  DELETE MTEMP1 MTEMP2 MTEMP3 MTEMP4 MTEMP5 MTEMP6 T1 TEM1 TEM2 TEM3 TEM4 TEM5 TEM6 TEM7 TEM8 RESLT RESLT2;
  RUN;
QUIT;
%MEND;


/*************************************************************************/
%MACRO PSICHAR(DATADEV,DATAVLD,DATAOUT,CHARVAR,WT);
DATA MTEMP1;
  SET &DATADEV. (KEEP=&CHARVAR. &WT.);
RUN;
DATA MTEMP2;
  SET &DATAVLD. (KEEP=&CHARVAR. &WT.);
RUN;
DATA MTEMP3;
  SET MTEMP1(IN=A) MTEMP2;
  IF A THEN TMFLAG='D'; ELSE TMFLAG='T';
RUN;
title "PSI OF &CHARVAR. Between Datasets: &DATADEV. vs. &DATAVLD.";
PROC FREQ DATA=MTEMP3(KEEP=&CHARVAR. &WT. TMFLAG);
  TABLE &CHARVAR.*TMFLAG /MISSING OUT=TEM1;
  WEIGHT &WT.;
RUN;
title;
DATA TEM2 TEM3;
  SET TEM1;
  IF TMFLAG='D' THEN OUTPUT TEM2;
  ELSE IF TMFLAG='T' THEN OUTPUT TEM3;
  DROP TMFLAG;
RUN;
PROC SORT DATA=TEM2;
BY &CHARVAR.;
RUN;
PROC SORT DATA=TEM3(RENAME=(PERCENT=PERCENT1 COUNT=COUNT1));
BY &CHARVAR.;
RUN;
DATA TEM4;
  MERGE TEM2 TEM3;
  BY &CHARVAR.;
RUN;
DATA TEM5;
  SET TEM4;
  ARRAY A COUNT COUNT1;
  DO OVER A;
     IF A=. THEN A=0;
  END;
RUN;
PROC SUMMARY DATA=TEM5;
  VAR COUNT COUNT1;
  OUTPUT OUT=TEM6 SUM(COUNT COUNT1)=COUNTTOT COUNT1TOT;
RUN;
DATA TEM7;
  IF _N_=1 THEN SET TEM6;
  SET TEM5;
  PERCENT=MAX(COUNT,0.1)/COUNTTOT;
  PERCENT1=MAX(COUNT1,0.1)/COUNT1TOT;
RUN;
DATA TEM8;
  SET TEM7;
  TOT=LOG(PERCENT/PERCENT1)*((PERCENT-PERCENT1));
RUN;
PROC SUMMARY DATA=TEM8;
  VAR TOT;
  OUTPUT OUT=RESLT SUM(TOT)=TOT;
RUN;
DATA RESLT2(KEEP=PSI NAME LABEL);
  LENGTH NAME $ 64 LABEL $ 100;
  SET RESLT;
  NAME="&CHARVAR.";
  LABEL="&LABEL.";
  RENAME TOT=PSI;
RUN;
PROC APPEND BASE=&DATAOUT.PSICHAR DATA=RESLT2 FORCE;
RUN;
PROC DATASETS NOLIST LIB=WORK;
  DELETE MTEMP1 MTEMP2 MTEMP3 TEM1 TEM2 TEM3 TEM4 TEM5 TEM6 TEM7 TEM8 RESLT RESLT2;
  RUN;
QUIT;
%MEND;


%macro PSICAL(DATFD,DATFV,DATFO);
DATA DATFDEV;
  SET &DATFD.(keep=&WGT. %varlst);
RUN;
DATA DATFVLD;
  SET &DATFV.(keep=&WGT. %varlst);
RUN;

PROC CONTENTS DATA=DATFDEV(drop=&WGT.) OUT=CONTNT (keep=TYPE NAME LABEL VARNUM);
RUN;
PROC SORT DATA=CONTNT NODUPKEY;
BY VARNUM;
RUN;
DATA _NULL_;
  SET CONTNT END=last;
  CALL SYMPUT("VAR"||COMPRESS(_N_),TRIM(LEFT(NAME)));
  CALL SYMPUT("LAB"||COMPRESS(_N_),TRIM(LEFT(LABEL)));
  CALL SYMPUT("TYP"||COMPRESS(_N_),TRIM(LEFT(TYPE)));
  IF TYPE=1 THEN CALL SYMPUT("NUMFLG","1"); ELSE CALL SYMPUT("CHARFLG","1");
  IF last THEN CALL SYMPUT("NVAR",COMPRESS(_N_));
RUN;
%do j=1 %to %eval(&NVAR.);
    %let LABEL="&&LAB&j..";
    %if "&&TYP&j.."="1" %then %PSINUM(DATFDEV,DATFVLD,RST,&&VAR&j..,10,&WGT.);
    %else %PSICHAR(DATFDEV,DATFVLD,RST,&&VAR&j..,&WGT.);
%end;
data &DATFO.;
  set %if %eval(&NUMFLG.>0) %then RSTPSINUM;
      %if %eval(&CHARFLG.>0) %then RSTPSICHAR;
      ;
run;
PROC DATASETS NOLIST LIB=WORK;
  DELETE DATFDEV DATFVLD CONTNT
    %if %eval(&NUMFLG.>0) %then RSTPSINUM;
    %if %eval(&CHARFLG.>0) %then RSTPSICHAR;
  ;
  RUN;
QUIT;
%mend;


libname RAW "D:\GY\18.4.9 ODBC_TC\scorecard\raw";

data RAW.dev;
do i=1 to 1000;
  x=ranuni(1)*10;
  y=ranuni(3)*20;
  if ranuni(6)<0.7 then C="A"; else C="B";
  wt=int(ranuni(2)*5);
  output;
end;
drop i;
run;

data RAW.out;
do i=1 to 206;
  x=ranuni(1)*10;
  y=ranuni(3)*60;
  if ranuni(6)<0.5 then C="A"; else C="B";
  wt=int(ranuni(8)*10);
  output;
end;
drop i;
run;


%let WGT=wt;

%macro varlst;
x
y
C
%mend;


%PSICAL(Raw.DEV,RAW.OUT,RAW.PSI);
